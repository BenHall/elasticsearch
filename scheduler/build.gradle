apply plugin: 'application'
apply plugin: 'docker'

mainClassName = "org.apache.mesos.elasticsearch.scheduler.ElasticsearchScheduler"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

dependencies {

    compile project(':commons')
    compile project(':elasticsearch-cloud-mesos')

    compile "commons-cli:commons-cli:1.0"

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'joda-time:joda-time:2.3'

}

jar {
    baseName = "es-scheduler"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest { attributes 'Main-Class': 'org.apache.mesos.elasticsearch.scheduler.ElasticsearchScheduler' }
}

task copyJar(type: Copy) {
    dependsOn   'jar'
    from        "build/libs/es-scheduler-${project.version}.jar"
    into        'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

task esSchedDocker(type: Docker) {
    dependsOn   copyJar
    dockerfile  'Dockerfile'
    contextDir  '.'
    useApi      false
    tag         'mesos/elasticsearch-scheduler'
    tagVersion  "$project.version"
}
build.dependsOn esSchedDocker
build.dependsOn copyJar
